/* NOTE: comment production omitted for brevity. `--` introduces a single-line
 * comment, while --[[ ... ]] introduces a multi-line comment (with optional
 * matching equal signs between the brackets, like Lua).
 *
 * this is used as reference to implement the parser - best effort will be
 * made to keep this up-to-date, but in doubt, the parser implementation is
 * the canonical version.
 *
 * Guiding lines:
 *   - JSON is valid syntax (language is a superset), need to handle edge cases
 *   like "\/" in strings (could it be the only exception? maybe some utf-16
 *   surrogates stuff 0xD800–0xDBFF?) This is why "null" is used for the nil
 *   value. (validate with https://github.com/nst/JSONTestSuite test suite)
 *   - Inspired by Lua, no need for statement terminators (not even newlines)
 *   - Given that tuples (and "..." to unpack array/tuple) are supported, no
 *   need for multi-value returns
 *   - Tuple values can be indexed using `v._0, v._1` so that those are valid
 *    names (don't need special case that would conflict with float literals).
 *   - Error handling: defer/catch blocks, "try"/"must" unary operator, "throw"
 *   statement.
 *   - Generally speaking, a block = a scope. GuardStmt and IfBindStmt have
 *   different scoping semantics.
 *   - No null-checking operators for now (`?.`, `?[`, etc.) as it's unclear
 *   if it would be super important in that language (especially with `try`)
 *   and it quickly adds up complexity (e.g. would it require `?(` for nil-
 *   checking function call too?).
 *   - To disambiguate ExprList and Tuple literal syntax, Tuple (and any iterable)
 *   unpacking is explicit (requires "..."), otherwise there's no unpacking or
 *   deconstruction happening. This means `x, y = (1, 2)` sets x to a tuple and y
 *   to nil (or results in parsing error, probably). `x, y, z = (1, 2)...` results
 *   in x=1, y=2, z=nil and no parsing error.
 *   - Function `self` binding only happens for class objects. Maybe a runtime
 *   function could be called to re/un-bind but only succeeds on functions that
 *   actually make use of `self` (and fail otherwise).
 *   - As a consequence, `self` is never implicit to lookup scope of a field.
*/

Chunk    = Block .
Block    = { Stmt } .

/*
 * IMPLEMENTATION NOTE:
 *
 * RetStmt, BreakStmt, ContinueStmt, ThrowStmt and GotoStmt are only valid as
 * last statement in a Block.
*/
Stmt     =  ";"
         |  ExprStmt
				 |  AssignStmt
				 |  AugAssignStmt
         |  IfStmt
         |  GuardStmt
         |  DoStmt
         |  ForStmt
         |  FuncStmt
         |  LabelStmt
         |  DeclStmt
         |  RetStmt
         |  BreakStmt
         |  ContinueStmt
         |  GotoStmt
         |  ClassStmt
				 /* error handling-related statements */
         |  DeferStmt
         |  CatchStmt
         |  ThrowStmt
         .

ExprStmt     = FuncCall .
ExprList     = Expr { "," Expr } .
Expr         = (SimpleExpr | unop Expr) { binop Expr } .
SimpleExpr   = float | int | string | "null" | "true" | "false" |
               Map | Array | Tuple | FuncExpr | ClassExpr |
							 SuffixedExpr .

Map          = "{" [Field {"," Field} [","]] "}" .
Field        = (name | string | "[" Expr "]") ":" Expr .
Array        = "[" [ Expr { "," Expr } [","] ] "]" .
Tuple        = "(" [ Expr ( "," | { "," Expr } [","] ) ] ")" .
FuncExpr     = "fn" Signature Body .
ClassExpr    = "class" InheritClause ClassBody .
SuffixedExpr = PrimaryExpr
                 { "." name | "[" Expr "]" | FuncArgs } .
PrimaryExpr  = name | "(" Expr ")" .
FuncArgs     = "(" [ExprList] ")" | Map | string | "!" .

Signature    = "(" ParamList ")" | "!" .
Body         = Block "end" .
ParamList    = [ {name ","} ((name | "..." name) [","]) ] .

AssignStmt   = SuffixedExpr {"," SuffixedExpr} "=" ExprList . // lhs has to be assignable
AugAssignStmt = SuffixedExpr  augbinop Expr . // lhs has to be assignable, evaluated only once
FuncCall     = SuffixedExpr . /* Must be a function call  */

IfStmt       = SimpleIfStmt | BindIfStmt .
SimpleIfStmt = "if" Cond "then" Block { "elseif" Cond "then" Block } [ "else" Block ] "end" .
BindIfStmt   = "if" DeclStmt "then" Block [ "else" Block ] "end" . // TODO: allow DeclStmt inside parens?
Cond         = Expr .

GuardStmt       = SimpleGuardStmt | BindGuardStmt . // block must exit with break/continue/return/throw.
SimpleGuardStmt = "guard" Cond "else" Block "end" .
BindGuardStmt   = "guard" DeclStmt "else" Block "end" .

DoStmt       = "do" Block "end" .

ForStmt          = CondForStmt | ThreePartForStmt | ForInStmt .
CondForStmt      = "for" [ Cond ] DoStmt . // equivalent to a "while" in other languages
ThreePartForStmt = "for" [ AssignStmt | AugAssignStmt | DeclStmt ] ";" [ Cond ] ";" [ Expr ] DoStmt .
ForInStmt        = "for" name { "," name } "in" ExprList DoStmt .

DeferStmt    = "defer" Block "end" .
CatchStmt    = "catch" Block "end" .
ThrowStmt    = "throw" [ Expr ] . // re-throws last error when no expression given, only allowed in "catch" blocks.

FuncStmt     = "fn" name Signature Body .
DeclStmt     = ( "let" | "const" ) name { "," name } [ "=" ExprList ] .
LabelStmt    = "::" name "::" .
RetStmt      = "return" [ Expr ] .
BreakStmt    = "break" [ name ] .
ContinueStmt = "continue" [ name ] .
GotoStmt     = "goto" name . // with multiple constraints (same func, not inside new variables, etc.)

ClassStmt     = "class" name InheritClause ClassBody .
InheritClause = ( "(" Expr ")" | "!" ) .
ClassBody     = { ( MethodDef | FieldDef ) } "end" .
MethodDef     = FuncStmt .
FieldDef      = DeclStmt .

/*
 * NOTE: terminals are handled by the scanner, but (sometimes simplified) rules
 * are documented below.
*/

binop = "+"   | "-"   | "*"  | "/"  | "//" | "^"  |
        "%"   | "&"   | "~"  | "|"  | ">>" | "<<" |
				"<"   | "<="  | ">"  | ">=" | "==" | "!=" |
				"and" | "or"                              .

augbinop = "+="   | "-="   | "*="  | "/="  | "//=" | "^="  |
					 "%="   | "&="   | "~="  | "|="  | ">>=" | "<<=" .

unop  = "+" | "-" | "~" | "not" | "try" | "must" | "#" | "..." . // "try" returns nil if the expression fails, "must" turns an exception in a fatal error. "..." unpacks a tuple/array(?).
name  = /* start with letter or underscore, next any letter, digit or "_" */ .

int         = decimal | hex | octal | binary  . // same as Go's rules, except leading 0 for octal
decimal     = ("0" … "9") [ ["_"] decdigits ] . // leading zeros are fine
hex         = "0" ("x" | "X") ["_"] hexdigits .
octal       = "0" ("o" | "O") ["_"] octdigits .
binary      = "0" ("b" | "B") ["_"] bindigits .
decdigits   = decdigit { ["_"] decdigit } .
hexdigits   = hexdigit { ["_"] hexdigit } .
octdigits   = octdigit { ["_"] octdigit } .
bindigits   = bindigit { ["_"] bindigit } .
decdigit    = "0" … "9" .
hexdigit    = "a" … "f" | "A" … "F" | decdigit .
octdigit    = "0" … "7" .
bindigit    = "0" … "1" .

float   = decf | hexf . // same as Go's rules
decf    = decdigits "." [decdigits] [decexp] |
          decdigits decexp                   |
          "." decdigits [decexp]             .
decexp  = ("e" | "E") ["+" | "-"] decdigits .
hexf    = "0" ("x" | "X") hexmant hexexp .
hexmant = ["_"] hexdigits "." [hexdigits] |
          ["_"] hexdigits                 |
          "." hexdigits                   .
hexexp  = ("p" | "P") ["+" | "-"] decdigits .

string   = short | long .
short    = ( "\"" { char } "\"" ) | ( "'" { char } "'" ) .
long     = "[" { "=" } "[" { rawchar } "]" { "=" } "]" . /* number of equal signs must match */
char     = esc | rawchar .
rawchar  = /* any byte */ .
esc      = "\\" ( "a" | "b" | "f" | "n" | "r" | "t" | "v" | "\\" | "/" | "\"" | "'" | "\n" | "z" ) |
           "\\x" hexdigit hexdigit                  |
           "\\" decdigit [ decdigit [ decdigit ] ]  |
           "\\u hexdigit hexdigit hexdigit hexdigit |
           "\\u{" hexdigit { hexdigit } "}"         .
